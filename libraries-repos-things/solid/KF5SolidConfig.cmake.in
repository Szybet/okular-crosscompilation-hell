# SolidConfig.cmake provides information about the installed Solid library.
# It can be used directly from CMake via find_package(Solid NO_MODULE)
#
# The following CMake variables are provided:
#   Solid_VERSION_MAJOR - the major version number of Solid
#   Solid_VERSION_MINOR - the minor version number of Solid
#   Solid_VERSION_PATCH - the patch version number of Solid
#   Solid_INCLUDE_DIRS  - the include directories to use
#   Solid_HAVE_UDev         - TRUE if device discovery via udev is supported
#
# Additionally, the following imported library targets are created, which may be used directly
# with target_link_libraries():
#   KF5::Solid - the Solid library

@PACKAGE_INIT@

set(SOLID_DBUS_INTERFACES_DIR "@PACKAGE_KDE_INSTALL_DBUSINTERFACEDIR@")

include(CMakeFindDependencyMacro)
find_dependency(Qt@QT_MAJOR_VERSION@Core @REQUIRED_QT_VERSION@)

if (NOT @BUILD_SHARED_LIBS@)
    find_dependency(Qt@QT_MAJOR_VERSION@Xml @REQUIRED_QT_VERSION@)
    find_dependency(Qt@QT_MAJOR_VERSION@Gui @REQUIRED_QT_VERSION@)

    if ("@Qt5DBus_FOUND@" OR "@Qt6DBus_FOUND@")
        find_dependency(Qt@QT_MAJOR_VERSION@DBus @REQUIRED_QT_VERSION@)
    endif()

    if (@HAVE_LIBMOUNT@)
        find_dependency(LibMount)
    endif()

    if (@UDev_FOUND@)
        find_dependency(UDev)
    endif()
endif()

include("${CMAKE_CURRENT_LIST_DIR}/KF5SolidTargets.cmake")
@PACKAGE_INCLUDE_QCHTARGETS@

# "public" variables:


set(Solid_HAVE_UDev @UDev_FOUND@ )

